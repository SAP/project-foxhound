/* -*- Mode: C++; tab-width: 2; indent-tabs-mode: nil; c-basic-offset: 2 -*- */
/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

include protocol PGMPContent;
include GMPTypes;

using GMPVideoCodec from "gmp-video-codec.h";
using GMPErr from "gmp-errors.h";

include "GMPMessageUtils.h";
include "GMPVideoDecoderChild.h";

namespace mozilla {
namespace gmp {

[ChildImpl="GMPVideoDecoderChild", ParentImpl=virtual]
async protocol PGMPVideoDecoder
{
  manager PGMPContent;
child:
  async InitDecode(GMPVideoCodec aCodecSettings,
                   uint8_t[] aCodecSpecific,
                   int32_t aCoreCount);
  async GiveShmem(Shmem aOutputShmem);
  async Decode(GMPVideoEncodedFrameData aInputFrame,
               Shmem aInputShmem,
               bool aMissingFrames,
               uint8_t[] aCodecSpecificInfo,
               int64_t aRenderTimeMs);
  async Reset();
  async Drain();
  async __delete__();

parent:
  async ReturnShmem(Shmem aInputShmem);
  async DecodedData(GMPVideoi420FrameData aDecodedFrame, uint8_t[] aDecodedData);
  async DecodedShmem(GMPVideoi420FrameData aDecodedFrame, Shmem aDecodedShmem);
  async ReceivedDecodedReferenceFrame(uint64_t aPictureId);
  async ReceivedDecodedFrame(uint64_t aPictureId);
  async InputDataExhausted();
  async DrainComplete();
  async ResetComplete();
  async Error(GMPErr aErr);
  async Shutdown();
};

} // namespace gmp
} // namespace mozilla
