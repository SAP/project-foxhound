/* -*- Mode: C++; tab-width: 8; indent-tabs-mode: nil; c-basic-offset: 2 -*- */
/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

#ifdef MOZ_WMF_MEDIA_ENGINE
  include protocol PMFMediaEngine;
#endif
#ifdef MOZ_WMF_CDM
  include protocol PMFCDM;
#endif

include protocol PTexture;
include protocol PRemoteDecoder;
include LayersSurfaces;
include PMediaDecoderParams;
include "mozilla/dom/MediaIPCUtils.h";
include "mozilla/layers/LayersMessageUtils.h";

using mozilla::VideoInfo from "MediaInfo.h";
using mozilla::AudioInfo from "MediaInfo.h";
using struct mozilla::layers::TextureFactoryIdentifier from "mozilla/layers/CompositorTypes.h";
using mozilla::CreateDecoderParams::OptionSet from "PlatformDecoderModule.h";
using mozilla::DecoderDoctorDiagnostics from "DecoderDoctorDiagnostics.h";
using mozilla::TrackingId from "PerformanceRecorder.h";

namespace mozilla {

struct VideoDecoderInfoIPDL
{
  VideoInfo videoInfo;
  float framerate;
};

union RemoteDecoderInfoIPDL
{
  AudioInfo;
  VideoDecoderInfoIPDL;
};

[NeedsOtherPid, ParentProc=any, ChildProc=anydom]
sync protocol PRemoteDecoderManager
{
  manages PRemoteDecoder;
#ifdef MOZ_WMF_MEDIA_ENGINE
  manages PMFMediaEngine;
#endif
#ifdef MOZ_WMF_CDM
  manages PMFCDM;
#endif

parent:
#ifdef MOZ_WMF_MEDIA_ENGINE
  async PMFMediaEngine();
#endif
#ifdef MOZ_WMF_CDM
  async PMFCDM(nsString keySystem);
#endif
  async PRemoteDecoder(RemoteDecoderInfoIPDL info,
                       OptionSet options,
                       TextureFactoryIdentifier? identifier,
                       uint64_t? mediaEngineId,
                       TrackingId? trackingId);

  sync Readback(SurfaceDescriptorGPUVideo sd) returns (SurfaceDescriptor aResult);

  async DeallocateSurfaceDescriptorGPUVideo(SurfaceDescriptorGPUVideo sd);
};

} // namespace mozilla
